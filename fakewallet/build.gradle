/*
 * Copyright (c) 2022 Solana Mobile Inc.
 */

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.jetbrains.kotlin.android)
    alias(libs.plugins.androidx.navigation.safeargs)
    alias(libs.plugins.androidx.compose.compiler)
}

android {
    namespace 'com.solanamobile.fakewallet'
    compileSdk 35

    defaultConfig {
        applicationId "com.solanamobile.fakewallet"
        minSdk 31
        targetSdk 35
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    flavorDimensions += "featureSet"
    signingConfigs {
        create("generic") {
            storeFile = file("../testkeystore/GenericKey")
            storePassword = 'generic123'
            keyAlias = 'generic'
            keyPassword = 'generic456'
        }
        create("privileged") {
            storeFile = file("../testkeystore/PrivilegedKey")
            storePassword = 'privileged123'
            keyAlias = 'privileged'
            keyPassword = 'privileged456'
        }
    }

    productFlavors {
        create("Generic") {
            dimension = "featureSet"
            signingConfig = signingConfigs["generic"]
        }
        create("Privileged") {
            dimension = "featureSet"
            signingConfig = signingConfigs["privileged"]
            minSdk 33 // Seed Vault privileged access requires API 33 or higher
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders["appIcon"] = "@mipmap/ic_launcher"
            manifestPlaceholders["appIconRound"] = "@mipmap/ic_launcher"
        }
        // See https://stackoverflow.com/a/75481789/2531686
        debug {
            signingConfig null
            manifestPlaceholders["appIcon"] = "@mipmap/ic_launcher_debug"
            manifestPlaceholders["appIconRound"] = "@mipmap/ic_launcher_debug"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    buildFeatures {
        compose true
        buildConfig true
    }
}

dependencies {
    implementation project(path: ':seedvault')
    implementation project(path: ':ui:apptheme')
    implementation libs.androidx.activity.ktx
    implementation libs.androidx.activity.compose
    implementation libs.androidx.appcompat
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.core.ktx
    implementation libs.androidx.fragment.ktx
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.lifecycle.viewmodel.ktx
    implementation libs.androidx.recyclerview
    implementation libs.android.material
    implementation libs.bouncycastle.bcprov.jdk18on
    implementation libs.kotlinx.coroutines.android

    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.compose.ui
    implementation libs.androidx.compose.material3

    testImplementation libs.junit
    androidTestImplementation libs.androidx.test.junit
    androidTestImplementation libs.androidx.test.espresso.core
}
